version "4.5"


#include "ZScript/biribiri_base.zsc"
#include "ZScript/biribiri_events.zsc"


class LightSpark : Actor
{
	double fscale;
	Default
	{
		Speed 2;
		+NOINTERACTION;
		RenderStyle "Normal";
		Alpha 1.0;
		Scale 0.25;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		A_SetScale(0.005);
		fscale = frandom(0.005,0.02);
	}
	
	States
	{
		Spawn:
			LSPR "#" 0 NoDelay
			{
				frame = random(1,12);
			}
		Spawn_Animate:
			LSPR "###" 1 Bright
			{
				Scale.X += fscale * 0.30;
				Scale.Y += fscale * 0.30;
			}
			LSPR O 1 Bright 
			{
				A_Fadeout(0.12);
				Scale.X -= fscale * 0.05;
				Scale.Y -= fscale * 0.05;
			}
		wait;
	}
}

class LightSmoke : Actor
{
	Default
	{
		Speed 2;
		+NOINTERACTION;
		RenderStyle "Add";
		Alpha 0.15;
		Scale 1.0;
		+ROLLSPRITE;
	}
	
	double rolldir;
	double fadespd;
	
	override void BeginPlay()
	{
		super.BeginPlay();
		rolldir = randompick(-1,1);
		fadespd = frandom(0.8,2.0);
		vel.x += frandom(-1,1);
		vel.y += frandom(-1,1);
		A_SetScale(frandom(0.50,1.75));
	}
	
	override void Tick()
	{
		super.Tick();
		vel.z += 0.01;
		vel *= 0.87;
		roll -= 1.2 * rolldir;
		alpha -= 0.01 * fadespd;
		A_SetScale(scale.x * 1.02);
		if(alpha <= 0.001) Destroy();
	}
	
	States
	{
		Spawn:
			SMOK A 1;
		loop;
	}
}


class LightBall : LightningActor
{
	Default
	{
		-LightningActor.DOFLASH;
		LightningActor.FlashTic 10;

		Scale 0.25;
		Translation "0:255=@80[4,8,76]";
	}
	
	States
	{
		Spawn:
			BEAM A 1 A_FadeOut(0.14);
		wait;
	}
}
class TestEmitter : Actor
{
	Default
	{
		Scale 0.25;
		+NOINTERACTION;
	}
	
	States
	{
		Spawn:
			TNT1 A 1 NoDelay
			{
				for(int s = 0; s < 360; s+= 45)
				{
					vector3 smokepos = (( cos(s),sin(s) ) * frandom(10,120), -8);
					vector3 finalpos = level.vec3offset(pos, smokepos);
					Spawn("LightSmoke", finalpos);
				}
			
				for(int i = 0; i < 360; i+=60)
				{
					if(random(0,3)==3)
					{
						vector2 angles = (angle+i+frandom(0,360),pitch+frandom(-90,90));
						BiriBiri.DoLightning(pos, angles, dist:200, 1.0, 10.0, 2.0, 20);
						if(random(0,3) == 3)
						{
							for(int s = 0; s < 40; s++)
							{
								double a = angles.x + frandom(-90,90);
								double p = angles.y + frandom(-90,90);
								vector3 sparkpos = (
									cos(a) * cos(p),
									sin(a) * cos(p),
									-sin(p)
								) * 200*frandom(0.01,0.60);
								vector3 finalpos = level.vec3offset(pos, sparkpos);
								Spawn("LightSpark", finalpos);
							}
						}
					}
				}
			}
		loop;
	}
}